# Step 10 (extract concepts)
# This query is run against the edited dataset
# (asgeirs edition from 2008-10-01)
# to extract 
#   concepts and relations to resources
# also converts skos:definition to skos:note.

base <http://detektor.sublima.computas.com:8180/detektor/> 
prefix rdfs: 		<http://www.w3.org/2000/01/rdf-schema#>
prefix skos:    <http://www.w3.org/2004/02/skos/core#> 
prefix sub:     <http://xmlns.computas.com/sublima#> 

CONSTRUCT { 
   ?topic a skos:Concept .
   ?topic skos:inScheme ?scheme .
   ?topic skos:prefLabel ?prefLabel .
   ?topic skos:altLabel ?altLabel .
   ?topic skos:hiddenLabel ?hiddenLabel .
   ?topic skos:broader ?broader .
   ?topic skos:narrower ?narrower .
   ?topic skos:note ?definition .
   ?topic skos:related ?related .
} 
WHERE {
   ?topic a skos:Concept .
   OPTIONAL {?topic skos:inScheme ?scheme .}
   OPTIONAL {?topic skos:prefLabel ?prefLabel .}
   OPTIONAL {?topic skos:altLabel ?altLabel .}
   OPTIONAL {?topic skos:hiddenLabel ?hiddenLabel .}
   OPTIONAL {?topic skos:broader ?broader .}
   OPTIONAL {?topic skos:narrower ?narrower .}
   OPTIONAL {?topic skos:definition ?definition .}
   OPTIONAL {?topic skos:related ?related .}
}